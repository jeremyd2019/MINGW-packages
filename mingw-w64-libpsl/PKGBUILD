# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=libpsl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.21.1
pkgrel=2
pkgdesc="Public Suffix List library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://github.com/rockdaboot/libpsl"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libidn2"
         "${MINGW_PACKAGE_PREFIX}-libunistring"
         "${MINGW_PACKAGE_PREFIX}-gettext")
#makedepends=("${MINGW_PACKAGE_PREFIX}-libxslt"
#             "${MINGW_PACKAGE_PREFIX}-python"
#             "${MINGW_PACKAGE_PREFIX}-gettext"
#             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
#             "${MINGW_PACKAGE_PREFIX}-meson"
#             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ac6ce1e1fbd4d0254c4ddb9d37f1fa99dec83619c1253328155206b896210d4c')

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  ../${_realname}-${pkgver}/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-static \
    --enable-shared \
    --enable-runtime=libidn2 \
    --enable-builtin=libidn2

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
